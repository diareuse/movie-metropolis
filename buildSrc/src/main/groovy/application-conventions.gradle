plugins {
    id 'com.android.application'
    id 'common-conventions'
    id 'signing-conventions'
}

def calendar = Calendar.getInstance()
def year = calendar.get(Calendar.YEAR)
def month = calendar.get(Calendar.MONTH) + 1
def dateVersion = "$year.$month"
def getType = { project ->
    switch (project.name) {
        case "app": return 0
        case "app-wear": return 1
        default: return 9
    }
}

android {

    namespace "movie.metropolis.app"

    defaultConfig {
        applicationId "movie.metropolis.app"
        versionCode 1 * 10 + getType(project)
        versionName dateVersion
    }

    buildTypes {

        debug {
            applicationIdSuffix ".dev"
            manifestPlaceholders += [
                    debug  : "true",
                    release: "false"
            ]
        }

        release {
            shrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            manifestPlaceholders += [
                    debug  : "false",
                    release: "true"
            ]
        }

    }

}