import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath "com.google.firebase:firebase-crashlytics-gradle:[2,3["
        classpath 'com.google.gms:google-services:[4,5['
    }
}
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.21' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.21' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '1.7.21' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.21' apply false
    id "com.android.library" version "[7,8[" apply false
    id "com.android.application" version "[7,8[" apply false
    id "com.google.dagger.hilt.android" version "[2,3[" apply false
    id "nl.neotech.plugin.rootcoverage" version "[1,2["
    id "io.gitlab.arturbosch.detekt" version "[1,2["
}

rootCoverage {
    excludes = ["wiki/depasquale/**"]

    generateCsv false
    generateHtml false
    generateXml true
}

task reportMerge(type: ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.sarif")
}

subprojects {
    plugins.withType(DetektPlugin) {
        tasks.withType(Detekt) { detektTask ->
            finalizedBy(reportMerge)

            reportMerge.configure { mergeTask ->
                mergeTask.input.from(detektTask.sarifReportFile)
            }
        }
    }
}